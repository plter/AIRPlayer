<?xml version="1.0"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" xmlns:s="library://ns.adobe.com/flex/spark"
                       creationComplete="creationCompleteHandler(event)" closing="closingHandler(event)" nativeDragEnter="nativeDragEnterHandler(event)" nativeDragDrop="nativeDragDropHandler(event)"
                       showStatusBar="false" title="{songName}" width="220" height="320" backgroundColor="white" alwaysInFront="true">

    <fx:Script><![CDATA[
        import com.plter.airplayer.Player;
        import com.plter.airplayer.Song;

        import mx.events.FlexEvent;

        [Bindable]
        private var songName:String;
        private var sound:Sound;

        private function nativeDragEnterHandler(event:NativeDragEvent):void {
            if (event.clipboard.hasFormat(ClipboardFormats.FILE_LIST_FORMAT)) {
                var files:Array = event.clipboard.getData(ClipboardFormats.FILE_LIST_FORMAT) as Array;

                if (files.length >= 1) {
                    var firstFile:File = files[0];
                    if (firstFile.extension.toLowerCase() == "mp3") {
                        NativeDragManager.acceptDragDrop(this);
                    }
                }
            }
        }

        private function nativeDragDropHandler(event:NativeDragEvent):void {
            if (event.clipboard.hasFormat(ClipboardFormats.FILE_LIST_FORMAT)) {
                var files:Array = event.clipboard.getData(ClipboardFormats.FILE_LIST_FORMAT) as Array;

                var firstFile:File = files[0];

                var song:Song = new Song(firstFile);
                Player.getInstance().addSong(song);
                songName = song.songName;
                Player.getInstance().playLast();
            }
        }

        private function songList_doubleClickHandler(event:MouseEvent):void {
            if (songList.selectedItem) {
                songName = songList.selectedItem.songName;
                Player.getInstance().playAtIndex(songList.selectedIndex);
            }
        }

        private function closingHandler(event:Event):void {
            Player.getInstance().savePlaylist();
        }

        private function creationCompleteHandler(event:FlexEvent):void {
            Player.getInstance().readPlayList();
            songName = songList.dataProvider[0].songName;
            Player.getInstance().playAtIndex(0);
        }
        ]]></fx:Script>

    <s:VGroup width="100%" height="100%">
        <s:Label text="{songName}" width="100%" textAlign="center"/>

        <s:List doubleClickEnabled="true" doubleClick="songList_doubleClickHandler(event)" width="100%" height="100%" id="songList" dataProvider="{Player.getInstance().songList}" labelField="songName"/>
    </s:VGroup>
</s:WindowedApplication>
